# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../dmsfwk.gni")

config("dms_sdk_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${dms_path}/common/include",
    "${dms_path}/interfaces/innerkits/common/include/",
    "${dms_path}/interfaces/innerkits/distributed_event/include/",
    "${dms_path}/services/dtbabilitymgr/include/",
    "${dms_path}/services/dtbschedmgr/include/",
    "${dms_path}/frameworks/native/distributed_extension/include",
    "${dms_path}/frameworks/native/distributed_extension/include/ipc"
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("distributed_sdk") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  sources = [
    "${dms_path}/interfaces/innerkits/distributed_event/src/dms_client.cpp",
    "${dms_path}/interfaces/innerkits/distributed_event/src/dms_handler.cpp",
    "${dms_path}/interfaces/innerkits/distributed_event/src/dms_listener_stub.cpp",
    "${dms_path}/interfaces/innerkits/distributed_event/src/dms_sa_client.cpp",
    "${dms_path}/interfaces/innerkits/distributed_event/src/dms_static_capability.cpp",
  ]

  public_configs = [
    ":dms_sdk_public_config",
    "${dms_path}/services/dtbschedmgr/test/resource:coverage_flags",
  ]

  ldflags = [
    "-fpie",
    "-Wl,-z,relro",
    "-Wl,-z,now",
  ]

  deps = [ "${dms_path}/frameworks/native/distributed_extension:distributed_extension_sa" ]

  external_deps = [
    "c_utils:utils",
    "device_info_manager:distributed_device_profile_common",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]
  public_external_deps = [ "ability_runtime:ability_manager" ]
  subsystem_name = "ability"
  innerapi_tags = [ "platformsdk" ]
  part_name = "dmsfwk"
}
